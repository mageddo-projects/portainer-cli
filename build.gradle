buildscript {
	repositories {
		mavenLocal()
	}
}
plugins {
	id "java"
	id "application"
	id "com.palantir.graal" version "0.3.0-2-g6c49b4d"
	id "com.mageddo.graal-reflection-configuration-generator" version "2.0.1"
	id 'net.researchgate.release' version '2.7.0'
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def className = "com.mageddo.portainer.cli.Application"

mainClassName = className

task debug(type: JavaExec){
	debug = true
	classpath = sourceSets.main.runtimeClasspath
	main = className
}

graal {
	mainClass className
	graalVersion(graalVMVersion)
	outputName(project.name)
	downloadBaseUrl("https://github.com/oracle/graal/releases/download/vm-19.0.2/graalvm-ce-linux-amd64-19.0.2.tar.gz#")
	option("-H:IncludeResources=")
	option("--allow-incomplete-classpath")
	option("-H:ReflectionConfigurationFiles=build/reflect.json")
	option("-H:+AllowVMInspection")
	option("--rerun-class-initialization-at-runtime=sun.security.jca.JCAUtil\$CachedSecureRandomHolder,javax.net.ssl.SSLContext")
	option("--initialize-at-run-time=com.sun.jndi.dns.DnsClient")
	option("-H:+ReportUnsupportedElementsAtRuntime")
	option("-H:EnableURLProtocols=http")
	option("-Dfile.encoding=UTF-8")
	option("--no-server")
	option("-J-Xmx5G")
}

dependencies {

	compile group: 'com.beust', name: 'jcommander', version: '1.72'
	compile group: 'com.mageddo.portainer', name: 'portainer-client', version: '1.2.0'
	compile "ch.qos.logback:logback-classic:1.2.3"
	
	compile('com.mageddo:rsl:1.0.0')
	annotationProcessor('com.mageddo:rsl-compiler:1.0.1')
	testAnnotationProcessor('com.mageddo:rsl-compiler:1.0.1')

//	tag::graal[]
	compile ("com.mageddo:graal-reflection-configuration:2.0.0")
	compileOnly("com.oracle.substratevm:svm:${graalVMVersion}")
	compile("org.graalvm.sdk:graal-sdk:${graalVMVersion}")
	//end::graal[]

	testCompile group: 'org.jboss.resteasy', name: 'resteasy-undertow', version: '3.6.2.Final'
	testCompile "junit:junit:4.12"

}

release {
	project.ext.set("release.useAutomaticVersion", true)
	git {
		requireBranch = ''
	}
	failOnCommitNeeded = false
	failOnPublishNeeded = false
	failOnUnversionedFiles = false
	buildTasks = []
}

nativeImage.dependsOn(tasks.reflectConfigFiles)
